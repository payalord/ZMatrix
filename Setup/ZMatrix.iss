; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

;#define UPGRADE
;#define WIN9X

#define MAJOR_VER "1"
#define MINOR_VER "5"
#define RELEASE_VER "2"

#define VIS_INSTALLER_NAME "ZMatrixVizModule_"+MAJOR_VER+"_"+MINOR_VER+"_"+RELEASE_VER+".exe"
#define VIS_INSTALLER_PATH "..\DistroVis\Output\"
#define VIS_INSTALLER_PATH_AND_NAME VIS_INSTALLER_PATH + VIS_INSTALLER_NAME

[Setup]
AppName=ZMatrix
AppMutex=ZMatrix
AppVerName=ZMatrix {#MAJOR_VER}.{#MINOR_VER}.{#RELEASE_VER}
AppPublisher=Happy Dude
AppPublisherURL=http://zmatrix.n3.net
AppVersion={#MAJOR_VER}.{#MINOR_VER}.{#RELEASE_VER}
DefaultDirName={pf}\ZMatrix
DefaultGroupName=ZMatrix
AllowNoIcons=yes
#ifdef WIN9X
MinVersion=4.0,0
#else
MinVersion=0,4.0
#endif
PrivilegesRequired=poweruser
LicenseFile=LICENSE.TXT
#ifdef WIN9X
SourceDir=..\Distro9x
#else
SourceDir=..\DistroNT
#endif
ShowTasksTreeLines=yes

#ifdef WIN9X
  #ifdef UPGRADE
  OutputBaseFilename=ZMatrixSetup9x_{#MAJOR_VER}_{#MINOR_VER}_{#RELEASE_VER}_Upgrade
  #else
  OutputBaseFilename=ZMatrixSetup9x_{#MAJOR_VER}_{#MINOR_VER}_{#RELEASE_VER}
  #endif
#else
  #ifdef UPGRADE
  OutputBaseFilename=ZMatrixSetupNT_{#MAJOR_VER}_{#MINOR_VER}_{#RELEASE_VER}_Upgrade
  #else
  OutputBaseFilename=ZMatrixSetupNT_{#MAJOR_VER}_{#MINOR_VER}_{#RELEASE_VER}
  #endif
#endif

[Components]
Name: "main"; Description: "Main Program Files"; Types: full compact custom; Flags: fixed
Name: "screensaver"; Description: "Screensaver Component"; Types: full
Name: "winampvis"; Description: "Winamp 2.x Visualization Component"; Types: full

[Files]
Source: "matrix.exe"; DestDir: "{app}"; Flags: ignoreversion; Components: main
#ifndef WIN9X
Source: "JapaneseSet.txt"; DestDir: "{app}"; Flags: ignoreversion; Components: main
#endif
Source: "MatrixCodeFontSet.txt"; DestDir: "{app}"; Flags: ignoreversion; Components: main
Source: "default.cfg"; DestDir: "{app}"; Flags: ignoreversion; Components: main
Source: "Config.dll"; DestDir: "{app}"; Flags: ignoreversion; Components: main
Source: "zsMatrix.dll"; DestDir: "{app}"; Flags: ignoreversion regserver; Components: main
Source: "MsgHook.dll"; DestDir: "{app}"; Flags: ignoreversion; Components: main
Source: "Matrix Code Font.ttf"; DestDir: "{fonts}"; FontInstall: "Matrix Code Font"; Flags: uninsneveruninstall ignoreversion; Components: main
Source: "ZMatrixHelp.chm"; DestDir: "{app}"; Components: main
Source: "readme.txt"; DestDir: "{app}";Flags: isreadme; Components: main
Source: "LICENSE.TXT"; DestDir: "{app}"; Components: main

Source: "ScreenSaver\ZMatrixSS.scr"; DestDir: "{win}"; Flags: ignoreversion; Components: screensaver

Source: {#VIS_INSTALLER_PATH_AND_NAME}; DestDir: "{app}"; Flags: dontcopy

[INI]
Filename: "{win}\ZMatrixSS.ini"; Section: "ZMatrixSS"; Key: "MatrixCommandLine"; String: "{app}\matrix.exe"
Filename: "{app}\ZMatrix.ini"; Section: "ZMatrix"; Key: "SetAsScreenSaverWhileRunning"; String: "true"; Tasks: setsswhilerunning
;Filename: "{app}\default.cfg"; Section: "General"; Key: "PriorityClass"; String: "HIGH_PRIORITY_CLASS"; Tasks: default_high_priority
;Filename: "{app}\default.cfg"; Section: "General"; Key: "PriorityClass"; String: "ABOVE_NORMAL_PRIORITY_CLASS"; Tasks: default_above_normal_priority
;Filename: "{app}\default.cfg"; Section: "General"; Key: "PriorityClass"; String: "NORMAL_PRIORITY_CLASS"; Tasks: default_normal_priority
;Filename: "{app}\default.cfg"; Section: "General"; Key: "PriorityClass"; String: "BELOW_NORMAL_PRIORITY_CLASS"; Tasks: default_below_normal_priority
;Filename: "{app}\default.cfg"; Section: "General"; Key: "PriorityClass"; String: "IDLE_PRIORITY_CLASS"; Tasks: default_idle_priority

Filename: "{app}\default.cfg"; Section: "Text"; Key: "CharSet"; String: "0,1"; Check: FontDoesntExist
Filename: "{app}\default.cfg"; Section: "Text"; Key: "FontName"; String: "MS Serif"; Check: FontDoesntExist

[Icons]
Name: "{group}\ZMatrix"; Filename: "{app}\matrix.exe"; WorkingDir: "{app}"
Name: "{group}\ZMatrix Help"; Filename: "{app}\ZMatrixHelp.chm"; WorkingDir: "{app}"
Name: "{group}\ZMatrix Homepage"; Filename: "http://zmatrix.n3.net"
Name: "{group}\ReadMe"; Filename: "{app}\readme.txt"
Name: "{group}\LICENSE"; Filename: "{app}\LICENSE.TXT"
Name: "{group}\Uninstall ZMatrix"; Filename: "{uninstallexe}"
Name: "{userstartup}\ZMatrix"; Filename: "{app}\matrix.exe"; WorkingDir: "{app}"; Tasks: autostart\user
Name: "{commonstartup}\ZMatrix"; Filename: "{app}\matrix.exe"; WorkingDir: "{app}"; Tasks: autostart\common

[Tasks]
Name: autostart; Description: "Automatic startup"; GroupDescription: "Automatic Startup Options:"
Name: autostart\user; Description: "Automatic startup for &current user only"; GroupDescription: "Automatic Startup Options:"; Flags: exclusive
Name: autostart\common; Description: "Automatic startup for &all users"; GroupDescription: "Automatic Startup Options:"; Flags: exclusive unchecked

;Name: default_high_priority; Description: "High Priority"; GroupDescription: "Default Program Priority:"; Flags: exclusive unchecked
;Name: default_above_normal_priority; Description: "Above Normal Priority"; GroupDescription: "Default Program Priority:"; Flags: exclusive unchecked
;Name: default_normal_priority; Description: "Normal Priority"; GroupDescription: "Default Program Priority:"; Flags: exclusive unchecked
;Name: default_below_normal_priority; Description: "Below Normal Priority"; GroupDescription: "Default Program Priority:"; Flags: exclusive unchecked
;Name: default_idle_priority; Description: "Idle Priority (recommended)"; GroupDescription: "Default Program Priority:"; Flags: exclusive

Name: setsswhilerunning; Description: "Always set as &screensaver while running"; GroupDescription: "Screensaver Options:"; Flags: unchecked

[Run]
Filename: "{app}\matrix.exe"; Description: "Launch ZMatrix"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: files; Name: "{win}\ZMatrixSS.ini"
Type: files; Name: "{app}\ZMatrix.ini"
Type: dirifempty; Name: "{app}"
Type: files; Name: "{code:GetWinampDir|'C:\Program Files\Winamp'}\Plugins\vis_zmx.dll"

[Code]

function FontDoesntExist(): Boolean;
var
  TempResult : Boolean;
begin
  TempResult := FileExists(ExpandConstant('{fonts}') + '\Matrix Code Font.ttf');
  Result := not TempResult;
end;

function IsShellDLLUpToDate(): Boolean;
var
  SearchPath, DLLPath : String;
  VersionMS, VersionLS: Cardinal;
begin
  SearchPath := ExpandConstant('{sys}') + ';' + ExpandConstant('{win}');
  DLLPath := FileSearch('shell32.dll',SearchPath);
  
  if Length(DLLPath) < 1  then
  begin
    Result := false;
  end else
  begin
    if GetVersionNumbers(DLLPath,VersionMS,VersionLS) then
    begin
    if VersionMS > 4 then
      Result := true
    else if VersionMS < 4 then
        Result := false
      else
      begin
        if VersionLS < 71 then
          Result := false
        else
          Result := true
      end;
    end else
    begin
      Result := false;
    end;
  end;
end;

function GetWinampDir(Default: String): String;
var
  WinampDir: String;
begin
  WinampDir := Default;

  RegQueryStringValue(HKEY_CURRENT_USER,'Software\Winamp','',WinampDir);
  
  Result := WinampDir;
end;


function LaunchNestedVisInstall(): Boolean;
var
  TempPath, WinampDir: String;
  ResultCode: Integer;
  TempBool, WinampInstalled: Boolean;
begin

  WinampInstalled := false;
  
  if RegQueryStringValue(HKEY_CURRENT_USER,'Software\Winamp','',WinampDir) then
  begin
    if DirExists(WinampDir) then
    begin
      WinampInstalled := true;
    end;
  end;
  
  if not WinampInstalled then
  begin
    WinampInstalled := (MsgBox('The installer did not detect the Winamp 2.x directory, are you sure you want to install the Winamp Visualization component? (if unsure, select No)', mbConfirmation, MB_YESNO) = idYes);
  end;
  
  if WinampInstalled then
  begin
    TempPath := ExpandConstant('{tmp}');
    TempBool := ExtractTemporaryFile('{#VIS_INSTALLER_NAME}');


    InstExec(TempPath + '\' + '{#VIS_INSTALLER_NAME}','',
             TempPath, True, False, SW_SHOWNORMAL, ResultCode);

    Result := true;
  end else
    Result := false;


end;

procedure CurStepChanged(CurStep: Integer);
begin
  if CurStep = csFinished then
  begin
    if srYes = ShouldProcessEntry('winampvis','') then
    begin
      LaunchNestedVisInstall();
    end;
  end;
end;

function InitializeSetup(): Boolean;
begin
  if not IsShellDLLUpToDate then
  begin
    MsgBox('ZMatrix requires the ''Windows Desktop Update'' provided by Microsoft to be installed.' +
           '  ZMatrix cannot continue installing without this update.' +
           '  Please install the desktop udpate, then try installing ZMatrix again'
           , mbInformation, MB_OK);
    Result := false;
  end else
    Result := true;

#ifdef UPGRADE
{
  if ( FontDoesntExist ) then
  begin
    Result := (MsgBox('This is an UPGRADE version of ZMatrix, and it doesn''t seem as though you''ve got the required previous version installed.' +
                      '  If you don''t have the required previous version, you should click No below then download and install the FULL version of ZMatrix.' +
                      '  Are you sure you want to continue with the installation?', mbConfirmation, MB_YESNO) = idYes);
  end
  else
  begin
    Result := True;
  end;
}
#endif

end;



